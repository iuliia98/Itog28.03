

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижение();
	
	РассчитатьОсновныеНачисления();    
	
	РассчитатьУдержания();
	
	РассчитатьОтпуск();   
	

	
	ВзаиморасчетыССотрудниками();
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижение(); 
	
	СформироватьНачисленияПоОкладу();
 
	РассчитатьДополнительныеНачисления();
	
КонецПроцедуры

Процедура СформироватьНачисленияПоОкладу()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗарплатыНачисления.Подразделение КАК Подразделение,
		|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВКМ_НачислениеЗарплатыНачисления.ГрафикРаботы КАК ГрафикРаботы,
		|	НАЧАЛОПЕРИОДА(ВКМ_НачислениеЗарплатыНачисления.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
		|	ВКМ_НачислениеЗарплатыНачисления.НомерСтроки КАК НомерСтроки,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗарплатыНачисления.ДатаНачала, МЕСЯЦ, -12), МЕСЯЦ) КАК БазовыйПериодНачало,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗарплатыНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ) КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТ_ОкладВДокументе
		|ИЗ
		|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &ВидРасчетаОклад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудников.Период КАК Период,
		|	ВТ_ОкладВДокументе.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ТекущиеПериодыОкладов
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОкладВДокументе КАК ВТ_ОкладВДокументе
		|		ПО ВТ_ОкладВДокументе.Сотрудник = ВКМ_УсловияОплатыСотрудников.Сотрудник
		|		И ВКМ_УсловияОплатыСотрудников.Период = ВТ_ОкладВДокументе.ПериодДействия
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ОкладВДокументе.НомерСтроки,
		|	ВКМ_УсловияОплатыСотрудников.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОкладВДокументе.Сотрудник КАК Сотрудник,
		|	ВТ_ОкладВДокументе.Подразделение КАК Подразделение,
		|	ВТ_ОкладВДокументе.ВидРасчета КАК ВидРасчета,
		|	ВТ_ОкладВДокументе.ДатаНачала КАК ПериодДействияНачало,
		|	ВТ_ОкладВДокументе.ДатаОкончания КАК ПериодДействияКонец,
		|	ВТ_ОкладВДокументе.ГрафикРаботы КАК ГрафикРаботы,
		|	ВКМ_УсловияОплатыСотрудников.Оклад КАК Оклад,
		|	ВТ_ОкладВДокументе.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	ВТ_ОкладВДокументе.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	ВТ_ОкладВДокументе.ПериодДействия КАК ПериодРегистрации
		|ИЗ
		|	ВТ_ОкладВДокументе КАК ВТ_ОкладВДокументе
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущиеПериодыОкладов КАК ВТ_ТекущиеПериодыОкладов
		|			ПО ВТ_ТекущиеПериодыОкладов.Период = ВКМ_УсловияОплатыСотрудников.Период
		|		ПО ВТ_ОкладВДокументе.НомерСтроки = ВТ_ТекущиеПериодыОкладов.НомерСтроки
		|		И ВТ_ОкладВДокументе.Сотрудник = ВКМ_УсловияОплатыСотрудников.Сотрудник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник,
		|	ВКМ_НачислениеЗарплатыНачисления.Подразделение,
		|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания,
		|	ВКМ_НачислениеЗарплатыНачисления.ГрафикРаботы,
		|	0,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗарплатыНачисления.ДатаНачала, МЕСЯЦ, -12), МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗарплатыНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ),
		|	ВКМ_НачислениеЗарплатыНачисления.Ссылка.Дата
		|ИЗ
		|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &ВидРасчетаОтпуск";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ВидРасчетаОтпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.ПериодРегистрации= Дата;
		Движение.НДФЛ = Движение.Оклад * 13 / 100;
		Если ВыборкаДетальныеЗаписи.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
		  Движение.БазовыйПериодНачало = ВыборкаДетальныеЗаписи.ПериодДействияНачало;
		  Движение.БазовыйПериодКонец = ВыборкаДетальныеЗаписи.ПериодДействияКонец;
		КонецЕсли;

	КонецЦикла;
	
Движения.ВКМ_ОсновныеНачисления.Записать();

	
КонецПроцедуры

Процедура РассчитатьОсновныеНачисления()

Запрос = Новый Запрос;
Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК Факт,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК Норма
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|ГДЕ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета = &Оклад
	|	И ВКМ_ОсновныеНачисленияДанныеГрафика.Регистратор = &Ссылка";

Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
Запрос.УстановитьПараметр("Ссылка", Ссылка);

РезультатЗапроса = Запрос.Выполнить();

ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		Движение.Оклад = Движение.Оклад * ВыборкаДетальныеЗаписи.Факт / ВыборкаДетальныеЗаписи.Норма; 
		Движение.ОтработаноДней = ВыборкаДетальныеЗаписи.Факт/8;     
		Движение.НДФЛ = Движение.Оклад * 13 / 100;

КонецЦикла;

Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);

	
КонецПроцедуры

Процедура РассчитатьДополнительныеНачисления()
	
	
Запрос = Новый Запрос;
Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ВыполненныеСтрудникомРаботыОбороты.СуммаКОплатеОборот КАК СуммаКОплатеОборот,
	|	ВКМ_ВыполненныеСтрудникомРаботыОбороты.Сотрудник КАК Сотрудник,
	|	ВКМ_ВыполненныеСтрудникомРаботыОбороты.Сотрудник.Подразделение КАК СотрудникПодразделение,
	|	ВКМ_ВыполненныеСтрудникомРаботыОбороты.ЧасовКОплатеОборот КАК ЧасовКОплатеОборот
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСтрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода,,) КАК
	|			ВКМ_ВыполненныеСтрудникомРаботыОбороты
	|		ПО ВКМ_УсловияОплатыСотрудников.Сотрудник = ВКМ_ВыполненныеСтрудникомРаботыОбороты.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВременнаяТаблица.СуммаКОплатеОборот КАК СуммаКОплатеОборот,
	|	ВременнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблица.СотрудникПодразделение КАК СотрудникПодразделение,
	|	ВременнаяТаблица.ЧасовКОплатеОборот КАК ЧасовКОплатеОборот,
	|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК ВременнаяТаблица
	|		ПО ВКМ_НачислениеЗарплатыНачисления.Сотрудник = ВременнаяТаблица.Сотрудник
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &ВидРасчета";

Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
Запрос.УстановитьПараметр("Ссылка", Ссылка);
Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия);

РезультатЗапроса = Запрос.Выполнить();

ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	
	
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ВыборкаДетальныеЗаписи.ВидРасчета; 
		Движение.Подразделение = ВыборкаДетальныеЗаписи.СотрудникПодразделение;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.Премия = ВыборкаДетальныеЗаписи.СуммаКОплатеОборот;
		Движение.НДФЛ = Движение.Премия * 13 / 100;

КонецЦикла;

	Движения.ВКМ_ДополнительныеНачисления.Записать();
	

КонецПроцедуры
	

Процедура РассчитатьУдержания()

Запрос = Новый Запрос;
Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.НДФЛ КАК НДФЛ,
	|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
	|	ВКМ_ДополнительныеНачисления.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВТ_НДФЛ
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.НДФЛ,
	|	ВКМ_ОсновныеНачисления.Сотрудник,
	|	ВКМ_ОсновныеНачисления.Подразделение
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_НДФЛ.НДФЛ) КАК НДФЛ,
	|	ВТ_НДФЛ.Сотрудник КАК Сотрудник,
	|	ВТ_НДФЛ.Подразделение КАК Подразделение
	|ИЗ
	|	ВТ_НДФЛ КАК ВТ_НДФЛ
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НДФЛ.Сотрудник,
	|	ВТ_НДФЛ.Подразделение";

Запрос.УстановитьПараметр("Регистратор", Ссылка);

РезультатЗапроса = Запрос.Выполнить();

ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ; 
		Движение.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.НДФЛ = ВыборкаДетальныеЗаписи.НДФЛ ;
КонецЦикла;

	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры


Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОкладБаза, 0) +
		|		ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.ПремияБаза, 0) КАК НачисленоЗаГод,
		|	ВКМ_ОсновныеНачисления.НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК План,
		|	РАЗНОСТЬДАТ(ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало,
		|		ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец, ДЕНЬ) КАК ДнейОтпуска,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеБазовыйПериод КАК ЗначениеБазовыйПериод,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОкладБаза, 0) КАК ОкладБаза,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.ПремияБаза, 0) КАК ПремияБаза
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Ссылка
		|		И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Ссылка
		|		И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
		|		И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|	И ВКМ_ОсновныеНачисления.ВидРасчета = &ВидРасчета";
	
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл	
		
			
			Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
			Движение.Оклад = (ВыборкаДетальныеЗаписи.НачисленоЗаГод / ВыборкаДетальныеЗаписи.ЗначениеБазовыйПериод) * ВыборкаДетальныеЗаписи.ДнейОтпуска;
			Движение.ОтработаноДней = ВыборкаДетальныеЗаписи.ДнейОтпуска+1;
			Движение.НДФЛ = Движение.Оклад * 13 / 100; 
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);
	
	
КонецПроцедуры



Процедура ВзаиморасчетыССотрудниками();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисления.Оклад, 0) КАК Поле1,
		|	NULL КАК Премия,
		|	NULL КАК НДФЛ
		|ПОМЕСТИТЬ ВТ_Сотрудник
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Сотрудник,
		|	NULL,
		|	ЕСТЬNULL(ВКМ_ДополнительныеНачисления.Премия, 0),
		|	NULL
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_Удержания.Сотрудник,
		|	NULL,
		|	NULL,
		|	ЕСТЬNULL(ВКМ_Удержания.НДФЛ, 0)
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Сотрудник.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(СУММА(ВТ_Сотрудник.Поле1),0) КАК Оклад,
		|	ЕСТЬNULL(СУММА(ВТ_Сотрудник.Премия),0) КАК Премия,
		|	ЕСТЬNULL(СУММА(ВТ_Сотрудник.НДФЛ),0) КАК НДФЛ
		|ИЗ
		|	ВТ_Сотрудник КАК ВТ_Сотрудник
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Сотрудник.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сумма = Выборка.Оклад  + Выборка.Премия - Выборка.НДФЛ; 
		
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать(); 

КонецПроцедуры



#КонецОбласти	

#КонецЕсли
